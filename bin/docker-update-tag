#!/usr/bin/env zsh

set -e

setopt EXTENDED_GLOB

readonly _SCRIPT_NAME=${0:t}

# parse option {{{
usage() {
cat <<- EOU
Usage: $_SCRIPT_NAME IMAGE

optional arguments:
  -i, --interactive  select tags to update using checklist
  -d                 select none
  -h, --help         print this help message
EOU
}

getopt_options=(
    help
    interactive
)

if ! getopt_tmp=$(POSIXLY_CORRECT=true getopt -q -o hdi --long ${(j:,:)getopt_options} -- "$@"); then
    # exit if error
    usage
    exit 1
fi

eval set -- $getopt_tmp
unset getopt_options
unset getopt_tmp

_DEFAULT_STATUS=on
_INTERACTIVE=0
while true; do
    case $1 in
        -h | --help ) usage; exit 0 ;;
        -d ) _DEFAULT_STATUS=off; shift ;;
        -i | --interactive) _INTERACTIVE=1; shift ;;
        -- ) shift; break ;;
    esac
done
# }}}

if [[ -z "$1" ]]; then
    >&2 echo "Image is missing"
    exit 1
fi

# sudo if user is not member of docker group
[[ ${$(id -Gn)[(r)docker]} == docker ]] && _DOCKER=docker || _DOCKER=(sudo docker)

typeset -a -U _IMAGES
_IMAGES=(${${${(f)"$($_DOCKER images | grep "^$1")"}/ ##/:}%% *})
if (($_INTERACTIVE)) && ((${#_IMAGES} > 0)) ; then
    (( ${+commands[dialog]} )) || {
        >&2 echo 'Command not found: dialog'
        exit 1
    }
    _CHECKLIST_PARAMS=('Select tag to upgrade' $((7 + ${#_IMAGES})) $((COLUMNS - 4)) ${#_IMAGES})
    for image in $_IMAGES; do
        _CHECKLIST_PARAMS+=($image '' $_DEFAULT_STATUS)
    done
    # filter tag by dialog command
    exec 3>&1 # Save the place that stdout (1) points to.
    _IMAGES=($(2>&1 1>&3 dialog --separate-output --keep-tite --title 'Docker images' --checklist "${(@)_CHECKLIST_PARAMS}"))
    exec 3>&- # Close FD #3.
fi

RUN() {
    echo ">> ${(q)@:q}"
    "$@"
}

echo 'Pull following images'
print -l $_IMAGES

for image in $_IMAGES; do
    RUN $_DOCKER pull $image
done
